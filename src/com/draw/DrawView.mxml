<?xml version="1.0" encoding="utf-8"?>
<s:View xmlns:fx="http://ns.adobe.com/mxml/2009" 
		xmlns:s="library://ns.adobe.com/flex/spark"
		addedToStage="addedToStageHandler(event)"
		removedFromStage="removedFromStageHandler(event)"
		width="100%" height="100%"> 
	
	<s:layout>
		<s:BasicLayout />
	</s:layout>
	
	<fx:Script>
		<![CDATA[
			import mx.core.UIComponent;
			import mx.events.EffectEvent;
			
			import spark.components.Image;
			import spark.components.SkinnableContainer;
			import spark.filters.DropShadowFilter;
			import spark.primitives.Rect;
			
			//http://saturnboy.com/2010/06/drawer-component-flex-4/
			//http://www.munkiihouse.com/?p=37
			
			public var hideActionBar : Boolean = false;
			
			private var isDrawerOpen : Boolean = false
			
			private var _frontView : SkinnableContainer;
			
			private var frontViewChanged : Boolean;
			
			public function set frontView(component : SkinnableContainer):void
			{
				_frontView = component;
				
				frontViewChanged = true;
			}
			
			public function get frontView():*
			{
				return _frontView;
			}
			
			private var _drawView : UIComponent;
			
			private var drawViewChanged : Boolean = false;

			public function get drawView():UIComponent
			{
				return _drawView;
			}

			public function set drawView(value:UIComponent):void
			{
				_drawView = value;
				
				drawViewChanged = true;
			}
			
			//[Inspectable(category="General", enumeration="top,bottom,left,right", defaultValue="bottom")]
			private var _drawerPosition : String = "bottom";
			
			private var drawerPositionChanged : Boolean = false;
			
			public function get drawerPosition():String
			{
				return _drawerPosition;
			}

			public function set drawerPosition(value:String):void
			{
				if (_drawerPosition != value)
				{
					_drawerPosition = value;
					
					drawerPositionChanged = true;	
				}
			}
			
			protected function addedToStageHandler(event:Event):void
			{
				Multitouch.inputMode = MultitouchInputMode.GESTURE;
				stage.addEventListener(TransformGestureEvent.GESTURE_PAN, onSwipeHandler);
				
				addEventListener(MouseEvent.CLICK, function(event : *):void
				{
					showDrawer();
					//holderFrontView.visible = false;
				});
			}
			
			private function onSwipeHandler(event : TransformGestureEvent):void
			{
				if (this.effectShowDraw.isPlaying)
					return;
				
				if (event.offsetY < 0)
				{
					this.showDrawer();
				}
			}
			
			protected function removedFromStageHandler(event:Event):void
			{
				stage.removeEventListener(TransformGestureEvent.GESTURE_PAN, onSwipeHandler);
			}
			
			override protected function commitProperties():void
			{
				super.commitProperties();
				
				if (frontViewChanged)
				{
					holderFrontView.addElement(frontView);
					moveFrontView.targets = [holderFrontView];
					
					var i : int = 0;
					var l : int = frontView.numElements;
					for(;i < l; i++)
					{
						var child : UIComponent = frontView.getElementAt(i) as UIComponent;
						this.fadeFrontView.targets.push(child);
					} 
					
					holderFrontView.addEventListener(MouseEvent.CLICK, onMouseClickHandler);
					
					frontViewChanged = false;
				}
				
				if (drawViewChanged)
				{
					holderDrawerView.addElement(drawView);
					
					setAnimationParameters();					
					drawViewChanged = false;
				}
				
				if (drawerPositionChanged)
				{
					 holderDrawerView.setStyle("top", undefined);
					holderDrawerView.setStyle("bottom", undefined);
					holderDrawerView.setStyle("left", undefined);
					holderDrawerView.setStyle("right", undefined);
					
					switch (_drawerPosition)
					{
						case "top": 
							holderDrawerView.setStyle("top", 0);
							break;
						case "bottom":
							holderDrawerView.setStyle("bottom", 0);
							break;
						case "left":
							holderDrawerView.setStyle("left", 0);
							break;
						case "right":
							holderDrawerView.setStyle("right", 0);
							break;
					} 
					
					setAnimationParameters();
					drawerPositionChanged = false;
				}
			}
			
			override protected function measure():void
			{
				super.measure();
				
				
			}
			
			override protected function updateDisplayList(w:Number, h:Number):void
			{
				super.updateDisplayList(w, h);
				
				holderFrontView.width = w;
				holderFrontView.height = h;
			}
			
			private function setAnimationParameters():void
			{
				switch (_drawerPosition)
				{
					case "top":
						moveFrontView.yFrom = 0;
						moveFrontView.yTo = drawView.height;
						shadowOnDrawer.angle = 0;
						break;
					case "bottom":
						moveFrontView.yFrom = 0;
						moveFrontView.yTo = drawView.height * (-1);
						shadowOnDrawer.angle = 90;
						break;
					case "left":
						moveFrontView.xFrom = 0;
						moveFrontView.xTo = drawView.width;
						break;
					case "right":
						moveFrontView.xFrom = 0;
						moveFrontView.xTo = drawView.width * (-1);
						break;
				}
			}
			
			protected function showDrawer():void
			{
				//this.navigator.hideActionBar(true);
				effectShowDraw.play();
			}
			
			protected function hideDrawer():void
			{
				//this.navigator.showActionBar(true);
				effectShowDraw.play(null, true);
			}
			
			private function onMouseClickHandler(event : MouseEvent):void
			{
				if (this.isDrawerOpen)
				{
					this.hideDrawer();
				}
			}
			
			protected function effectShowDraw_effectEndHandler(event:EffectEvent):void
			{
				this.isDrawerOpen = !isDrawerOpen;
				
				for each (var child : UIComponent in this.fadeFrontView.targets)
				{
					child.enabled = (this.isDrawerOpen == false) 	
				}
			}
			
			protected function onCoverImageClickHandler(event : MouseEvent):void
			{
				this.hideDrawer();
			}
			
			protected function effectShowDraw_effectStartHandler(event:EffectEvent):void
			{
				
			}
			
		]]>
	</fx:Script>
	
	<fx:Declarations>
		<s:Parallel id="effectShowDraw" 
					duration="300"
					suspendBackgroundProcessing="true"
					effectStart="effectShowDraw_effectStartHandler(event)"
					effectEnd="effectShowDraw_effectEndHandler(event)">
			<s:children>
				<s:Move id="moveFrontView"/> 
				<s:Fade id="fadeFrontView" 
						alphaFrom="1" 
						alphaTo=".3"/>
			</s:children>
		</s:Parallel>
		
		<s:DropShadowFilter id="shadowOnDrawer" color="#000000" distance="5" blurX="10" blurY="10" angle="90" alpha=".6"/>
	</fx:Declarations>
	
	<s:Group id="holderDrawerView"/>
	
	<s:Group id="holderFrontView"
			 clipAndEnableScrolling="true"
			 filters="{ [ shadowOnDrawer ] }"/>
</s:View>
